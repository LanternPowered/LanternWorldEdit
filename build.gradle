plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id "org.spongepowered.plugin" version "0.8.1"
}

// Basic project information
group = 'org.lanterpowered'
archivesBaseName = 'lantern-worldedit'
version = '1.0.0-SNAPSHOT'

defaultTasks 'licenseFormat', 'build'

sponge {
    plugin {
        id = "lantern_worldedit"
        meta {
            name = project.name
            description = project.description
            authors = [ "Cybermaxke" ]
        }
    }
}

// Repositories
repositories {
    mavenCentral()
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        url "${project.projectDir}/library/"
    }
    maven {
        url 'https://maven.sk89q.com/repo/'
    }
}

// Project dependencies
dependencies {
    compile 'org.spongepowered:spongeapi:6.0.0-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-sponge:6.1.7-SNAPSHOT'
    compile 'org.lanternpowered:lanternserver:1.0.0'
}

jar {
	classifier = ''
}

processResources {
    from 'LICENSE.txt'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
    from sourceSets.main.allSource
}
assemble.dependsOn sourceJar

license {
    header project.file('HEADER.txt')
    sourceSets = project.sourceSets
    include '**/*.java'

    strictCheck true
    ignoreFailures = false

    ext {
        name = project.name
        url = project.url
        organization = project.organization
    }

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// Gradle version used for generating the Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}